#lang pollen

â—Šdocmodule["strings" #:noimport #t #:friendly-title "Strings"]{

â—Štype-spec["String"]{

The type of string values.}
                                                               
A â—Špyret{String} is a fixed-length array of characters.  This includes not only letters in
the Latin alphabet and numerals, but any Unicode character, including languages
using non-Latin characters, such as Arabic, Russian or Chinese, as well as emoji
defined in the Unicode specification.

â—Š(image "./valid-string.png")

â—Šmargin-note{If you click on printed strings in the interactive window,
the display will toggle between the character itself and the relevant
Unicode escape code or codes.}

â—Š(image "codes.png")

Internally, a Pyret â—Špyret{String} is implemented as a JavaScript
â—Štt{JSString}.  See the â—Šseclink["runtime"] for more context.

â—Šmargin-note{One implication of how JavaScript handles Unicode characters is
that characters that are identified by a Unicode code point greater than
65535 are sometimes treated as two characters by Pyret, as noted below.}


â—Šsection{String Functions}

  â—Šfunction["string-equal" #:contract (a-ftype (a-var-type "s1" S) (a-var-type "s2" S) B) #:return B]

Returns â—Špyret{true} if the two strings are equal.
  
â—Šexamples{
check:
  string-equal("abc", "abc") is true
  "abc" is%(string-equal) "abc"
  "abc" == "abc" is true
  string-equal("ab", "abc") is false
  string-equal("abc     ", "abc") is false
end
}

  â—Šfunction["string-contains" #:contract (a-ftype (a-var-type "string-to-search" S) (a-var-type "string-to-find" S) B) #:return B]

Returns â—Špyret{true} if â—Špyret{string-to-find} is contained in
â—Špyret{string-to-search}.  Returns â—Špyret{true} if an empty string is passed as
â—Špyret{string-to-find}.
  
â—Šexamples{
check:
  string-contains("Ahoy, world!", "world") is true
  string-contains("Ahoy, World!", "world") is false
  string-contains("world", "Ahoy world") is false
  string-contains("same string", "same string") is true
  string-contains("", "") is true
  string-contains("any string", "") is true
end
}

  â—Šfunction["string-find" #:contract (a-ftype (a-var-type "original-string" S) (a-var-type "string-to-find" S) B) #:return N]

Return the left-most index (starting from â—Špyret{0}) where the second argument is found in the first string.

If the string is not found, this raises an exception. Therefore, use this function only when you expect the second argument to be found in the first one.
If you aren't sure, use â—Špyret{string-find-opt}.
  
â—Šexamples{
check:
  string-find("Hello", "ello") is 1
  string-find("Hello", "H") is 0
  string-find("Hello", "World") raises ""
end
}

  â—Šfunction["string-find-opt" #:contract (a-ftype (a-var-type "original-string" S) (a-var-type "string-to-find" S) B) #:return N]

Return the left-most index (starting from â—Špyret{0}) where the second argument is found in the first string.

This always returns an â—Špyret{Option} value. Therefore, this is useful when you aren't sure whether the second argument will be found in the first or not.
If you are confident it will be present, consider using â—Špyret{string-find}, which returns the number that you can directly use.

â—Šexamples{
check:
  string-find("Hello", "ello") is some(1)
  string-find("Hello", "H") is some(0)
  string-find("Hello", "World") is none
end
}

  â—Šfunction["string-append" #:contract (a-ftype (a-var-type "front" S) (a-var-type "back" S) B) #:return S]

Returns a â—Špyret{String} where â—Špyret{back} is added to the right of
â—Špyret{front}.
  
â—Šexamples{
check:
  string-append("a", "b") is "ab"
  string-append("same", "same") is "samesame"
  string-append("", "") is ""
  string-append("", "a") is "a"
  string-append("a", "") is "a"
end
}

â—Šform["+ (concatenation)" "front + back"]

When â—Špyret{front} and â—Špyret{back} are strings, has the same meaning as
â—Špyret-id{string-append}.

  â—Šfunction["string-length" #:contract (a-ftype (a-var-type "s" S) N) #:return N]

Returns the number of characters in the string.

â—Šmargin-note{â—Špyret{string-length} reports a count of â—Špyret{2}
for code points over 65535.}

â—Šexamples{
check:
  string-length("") is 0
  string-length("    ") is 4
  string-length("four") is 4
  string-length("ğŸ") is 2
end
}

  â—Šfunction["string-to-number" #:contract (a-ftype (a-var-type "s" S) (O-of N)) #:return (O-of N)]

Converts the argument string to a number, returning â—Špyret-id["none" "option"]
if it is not a valid numeric string, and â—Špyret-id["some" "option"] number if it is.

â—Špyret-id{string-to-number} is strict about its inputs, and recognizes exactly
the same numbers that Pyret itself does: no surrounding whitespace, extra
punctuation, or trailing characters are allowed.

â—Šexamples{
check:
  string-to-number("100") is some(100)
  string-to-number("not-a-number") is none
  string-to-number(" 100") is none
  string-to-number("100abc") is none
  string-to-number("1,000") is none
  string-to-number("1-800-555-1212") is none
end
}

  â—Šfunction["string-repeat" #:contract (a-ftype (a-var-type "s" S) (a-var-type "n" N) S) #:return S]

â—Šexamples{
check:
  string-repeat("a", 5) is "aaaaa"
  string-repeat("", 1000000) is ""
  string-repeat("word ", 3) is "word word word "
  string-repeat("long string", 0) is ""
end
}

  â—Šfunction["string-substring" #:contract (a-ftype (a-var-type "s" S) (a-var-type "start-index" N) (a-var-type "end-index" N) S) #:return S]

Returns a new string created from the characters of the input string, starting
from â—Špyret{start-index} (inclusive) and ending at â—Špyret{end-index} (exclusive).
Raises an exception if â—Špyret{start-index} is greater than â—Špyret{end-index}, if â—Špyret{start-index}
is greater than the length of the string, or if â—Špyret{end-index} is less than 0.

The returned string always has length â—Špyret{end-index} - â—Špyret{start-index}.

â—Šmargin-note{â—Špyret{String} indexes are counted starting from zero for the
first character.}

â—Šexamples{
check:
  string-substring("just the first", 0, 1) is "j"
  string-substring("same index", 4, 4) is ""
  
  tws = "length is 12"
  string-substring(tws, 4, 6) is "th"
  string-substring(tws, string-length(tws) - 1, string-length(tws)) is "2"
  string-substring(tws, 6, 4) raises "index"
  string-substring(tws, 6, 13) raises "index"
  string-substring(tws, 13, 6) raises "index"
  string-substring(tws, -1, 10) raises "index"
end
}

  â—Šfunction["string-index-of" #:contract (a-ftype (a-var-type "original-string" S) (a-var-type "string-to-find" S) N) #:return N]

  Returns the index from the beginning of the string where
  â—Špyret{string-to-find} â—Šemph{first} appears, or â—Špyret{-1} if the string
  isn't found.

â—Šexamples{
check:
  string-index-of("Pyret", "P") is 0
  string-index-of("012ğŸ¤‘45", "ğŸ¤‘") is 3
  string-index-of("ğŸ¤”ğŸ¤”ğŸ¤”", "ğŸ¤’") is -1
end
}
  
  â—Šfunction["string-replace" #:contract (a-ftype (a-var-type "original-string" S) (a-var-type "string-to-find" S) (a-var-type "replacement-string" S) S) #:return S]

Returns a string where each instance of â—Špyret{string-to-find} in the
â—Špyret{original-string} is replaced by â—Špyret{replacement-string}.

If the string to find is empty â—Špyret{""}, the â—Špyret{replacement-string}
will be added between characters but not at the beginning or end of the
string.
  
â—Šexamples{
check:
  string-replace("spaces to hyphens", " ", "-") is "spaces-to-hyphens"
  string-replace("remove: the: colons", ":", "") is "remove the colons"
  string-replace("ğŸ˜ŠğŸ˜ŠğŸ¤•ğŸ˜Š", "ğŸ¤•", "ğŸ˜Š") is "ğŸ˜ŠğŸ˜ŠğŸ˜ŠğŸ˜Š"
  string-replace("rinky dinky", "inky", "azzle") is "razzle dazzle"
  string-replace("a string", "not found", "not replaced") is "a string"
  string-replace("", "", "c") is ""
  string-replace("aaa", "", "b") is "ababa"
end
}

  â—Šfunction["string-split" #:contract (a-ftype (a-var-type "original-string" S) (a-var-type "string-to-split-on" S) (L-of S)) #:return (L-of S)]

  Searches for â—Špyret{string-to-split-on} in â—Špyret{original-string}.  If it is not found,
  returns a â—Špyret-id["List" "lists"] containing â—Špyret{original-string} as its
  single element.

  If it is found, it returns a two-element â—Špyret-id["List" "lists"], whose
  first element is the portion of the string before â—Šemph{first} occurence of
  â—Špyret{string-to-split-on}.  The second element contains the portion of the string
  after.  The â—Špyret{string-to-split-on} is â—Šbold{not} included in either string.  The
  string before and the string after might be empty.

  For splitting beyond the first occurence of the string, see
  â—Špyret-id["string-split-all"].

â—Šexamples{
check:
  string-split("string", "not found") is [list: "string"]
  string-split("string", "g") is [list: "strin", ""]
  string-split("string", "") is [list: "", "string"]
  string-split("a-b-c", "-") is [list: "a", "b-c"]
end
}

  â—Šfunction["string-split-all" #:contract (a-ftype (a-var-type "original-string" S) (a-var-type "string-to-split-on" S) (L-of S)) #:return (L-of S)]

  Searches for â—Špyret{string-to-split-on} in â—Špyret{original-string}.  If it is not found,
  returns a â—Špyret-id["List" "lists"] containing â—Špyret{original-string} as its
  single element.

  If it is found, it returns a â—Špyret-id["List" "lists"], whose elements are
  the portions of the string that appear in between occurences of
  â—Špyret{string-to-split-on}.  A match at the beginning or end of the string will add
  an empty string to the beginning or end of the list, respectively.  The empty
  string matches in between every pair of characters.

â—Šexamples{
check:
  string-split-all("string", "not found") is [list: "string"]
  string-split-all("a-b-c", "-") is [list: "a", "b", "c"]
  string-split-all("split on spaces", " ") is [list: "split", "on", "spaces"]
  string-split-all("explode", "") is [list: "e", "x", "p", "l", "o", "d", "e"]
  string-split-all("bananarama", "na") is [list: "ba", "", "rama"]
  string-split-all("bananarama", "a") is [list: "b", "n", "n", "r", "m", ""]
end
}
  â—Šfunction["string-explode" #:contract (a-ftype (a-var-type "s" S)  (L-of S)) #:return (L-of S)]

  A shorthand for â—Špyret{string-split-all(s, "")}.

  â—Šfunction["string-char-at" #:contract (a-ftype (a-var-type "s" S) (a-var-type "n" N) S) #:return S]

Returns a â—Špyret{String} containing the character at the string index â—Špyret{n}
from â—Špyret{String} â—Špyret{n}.

â—Šexamples{
check:
  string-char-at("abc", 1) is "b"
  string-char-at("a", 0) is "a"
end
}

  â—Šfunction["string-toupper" #:contract (a-ftype (a-var-type "s" S)  S) #:return S]

  The same as â—Špyret{string-to-upper}.

  â—Šfunction["string-to-upper" #:contract (a-ftype (a-var-type "s" S)  S) #:return S]

â—Šmargin-note{Pyret uses JavaScript's built-in string operations, and so will
have the same behavior as â—Šlink["https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference/Global_Objects/String/toUpperCase" "toUpperCase"].}
Convert a string to all uppercase characters.  Punctuation and other characters
without an uppercase equivalent are left alone.  Note that because of
characters like â—Špyret{ÃŸ}, the length of the input is not guaranteed to
match the length of the output.

â—Šexamples{
check:
  string-to-upper("a") is "A"
  string-to-upper("I'm not yelling!") is "I'M NOT YELLING!"
  string-to-upper("ÃŸ") is "SS"
  string-to-upper("Î»Î±Î¼Î²Î´Î±") is "Î›Î‘ÎœÎ’Î”Î‘"
  string-to-upper("ğŸ˜Š") is "ğŸ˜Š"
  string-to-upper(" ï·µâ€") is " ï·µâ€"
end
}

When performing case-insensitive comparisons, it can be useful to convert both
strings to uppercase first:


â—Šexamples{
check:
  string-to-upper("E.E. Cummings") is string-to-upper("e.e. cummings")
end
}


  â—Šfunction["string-tolower" #:contract (a-ftype (a-var-type "s" S)  S) #:return S]

  The same as â—Špyret{string-to-lower}.

  â—Šfunction["string-to-lower" #:contract (a-ftype (a-var-type "s" S)  S) #:return S]

Converts a â—Špyret{String} to all lower case.
  
â—Šexamples{
check:
  string-to-lower("A") is "a"
  string-to-lower("I'M NOT YELLING!") is "i'm not yelling!"
  string-to-lower("SS") is "ss"
  string-to-lower("Î›Î‘ÎœÎ’Î”Î‘") is "Î»Î±Î¼Î²Î´Î±"
end
}

  â—Šfunction["string-to-code-point" #:contract (a-ftype (a-var-type "s" S)  N) #:return N]

  â—Šnote{For strings
  that contain a single character whose code point is greater than
  â—Špyret{65535}, this function raises an error.
  To get multiple codes at once for a longer string (or a string with larger code points), use
  â—Špyret-id{string-to-code-points}.}

  Converts â—Špyret{s}, which must be a single-character â—Špyret{String}, to a character
  code -- a â—Špyret{Number} corresponding to its Unicode code point
  (â—Šurl["http://en.wikipedia.org/wiki/Code_point"]).
  

  â—Šexamples{
check:
  string-to-code-point("a") is 97
  string-to-code-point("\n") is 10
  string-to-code-point("Î»") is 955
end
  }

  â—Šfunction["string-to-code-points" #:contract (a-ftype (a-var-type "codes" S) (L-of N)) #:return (L-of N)]

  Converts the string (of any length) to a list of code points.  Note that
  strings are encoded in such a way that some characters correspond to two code
  points (see the note in â—Špyret-id{string-to-code-point}).

â—Šexamples{
check:
  string-to-code-points("") is [list:]
  string-to-code-points("abc") is [list: 97, 98, 99]
  string-to-code-points("ğŸ˜Š") is [list: 55357, 56842]
  string-to-code-points("ğ„") is [list: 55348, 56606] 
end
}

  â—Šfunction["string-from-code-point" #:contract (a-ftype (a-var-type "code" N) S) #:return S]

  â—Šnote{Code points greater than 65535 are not supported.  You must encode
  higher code points with a â—Šlink["http://en.wikipedia.org/wiki/UTF-16"
  "surrogate pair"] in combination with
  â—Špyret-id{string-from-code-points} and â—Špyret-id{string-to-code-points}.}

  Converts the code point â—Špyret{code} to a Pyret string.

â—Šexamples{
check:
  string-from-code-point(97) is "a"
  string-from-code-point(10) is "\n"
  string-from-code-point(955) is "Î»"
end
}

  â—Šfunction["string-from-code-points" #:contract (a-ftype (a-var-type "codes" (L-of N)) S) #:return S]

  Converts from a list of code points to a Pyret string.

â—Šexamples{
check:
  string-from-code-points([list:]) is ""
  string-from-code-points([list: 97, 98, 99]) is "abc"
  string-from-code-points([list: 55348, 56606]) is "ğ„"
end
}

}
